apply plugin: 'android-sdk-manager'
apply plugin: AndroidStubPlugin
apply plugin: 'java'
apply plugin: "jacoco"
apply from: '../gradle-mvn-push.gradle'

def Properties props = new Properties()
props.load(new FileInputStream(file('../local.properties')))

def android = [
		sdk   : props["sdk.dir"],
		target: 'android-19'
]

repositories {
	mavenCentral()
	maven {
		url "${android.sdk}/extras/android/m2repository/"
	}
}

dependencies {
	compile files("${android.sdk}/platforms/${android.target}/android.jar")

	compile 'io.reactivex:rxjava:1.0.0-rc.3'

	testCompile project(':compiler')

	testCompile 'com.squareup.assertj:assertj-android:1.0.0'
	testCompile 'junit:junit:4.11'
	testCompile 'org.robolectric:robolectric:2.3'
}

test {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.withType(Test) {
	scanForTestClasses = false
	include "**/*Test.class"
}

// This plugin's only purpose is to provide the android-sdk-manager plugin the properties it expects from the android
// plugin. This is necessary because this is a Java project reliant on the Android library, and not an Android project.
class AndroidStubPlugin implements Plugin<Project> {
	void apply(Project project) {
		project.extensions.create('android', AndroidStubPluginExtension)
	}
}

class AndroidStubPluginExtension {
	def compileSdkVersion = 19
}
